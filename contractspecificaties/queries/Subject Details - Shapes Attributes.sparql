PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl:  <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd:  <http://www.w3.org/2001/XMLSchema#>
PREFIX nen2660: <https://w3id.org/nen2660/def#>
PREFIX sh: <http://www.w3.org/ns/shacl#>

SELECT DISTINCT ?group_text ?group_iri ?entry_text ?entry_iri ?entry_badge ?entry_icon
WHERE {

#########################
# This query returns the 'Attributes' (as defined in the BPL) for a given concept.
#
# INPUT:
#   ?term_iri: The IRI of the concept
#########################

    ?term_iri   rdfs:subClassOf*    ?super .
    ?shape      sh:targetClass      ?super .
    ?shape      sh:property         ?property .   
    ?property   sh:path             ?path  .
   
    OPTIONAL{   ?property  sh:maxCount     ?maxCount } .
    OPTIONAL{   ?property  sh:minCount     ?minCount } .
    OPTIONAL{   ?path      skos:prefLabel  ?pathLabel} .
    OPTIONAL{   ?property  sh:datatype     ?datatypeTarget}.
    OPTIONAL{   ?property  sh:in           [rdf:rest*/rdf:first ?enum]}.
    FILTER NOT EXISTS { ?property  sh:class  ?targetClass }  . 
    
    BIND (CONCAT(IF(BOUND(?minCount),STR(?minCount),"0"),":",IF(BOUND(?maxCount),STR(?maxCount),"n")) AS ?entry_badge) .

    BIND (REPLACE(STR(?enum), "^.*/([^/]*)$", "$1") as ?enumLabel)
    BIND (IF(BOUND(?datatypeTarget), ?datatypeTarget, ?enumLabel) AS ?target)
    BIND (REPLACE(STR(?datatypeTarget), STR(xsd:), 'xsd:') AS ?datatypeTargetLabel )
    BIND (COALESCE(?enumLabel,?datatypeTargetLabel) AS ?targetLabel)

    BIND (?pathLabel AS ?group_text) 
    BIND (?path AS ?group_iri) 
    BIND (?targetLabel AS ?entry_text) 
    BIND (?target AS ?entry_iri) 

    BIND ("icon-term-type-lacestype" AS ?entry_icon) .
   
}
ORDER BY ?group_text ?group_iri ?entry_text ?entry_iri
